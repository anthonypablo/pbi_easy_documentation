Cat치logo de Dados = 
VAR colunas =
    SELECTCOLUMNS (
        FILTER (
            INFO.VIEW.COLUMNS (),
            [Table] <> "Cat치logo de Dados"
                && NOT ( [IsHidden] )
        ),
        "Categoria", "Coluna",
        "Nome", [Name],
        "Descricao", IF(
                        [IsKey],
                        "Coluna chave de relacionamento.",
                        ""
                    ) & [Description],
        "Tipo", [DataType] & " - " & [DataCategory],
        "Origem", [Table],
        "Expressao", [Expression],
        "Lineage Tag", [LineageTag]
    )
VAR medidas =
    SELECTCOLUMNS (
        FILTER (
            INFO.VIEW.MEASURES (),
            [Table] <> "Cat치logo de Dados"
                && NOT ( [IsHidden] )
        ),
        "Categoria", "Medida",
        "Nome", [Name],
        "Descricao", [Description],
        "Tipo", [FormatStringDefinition],
        "Origem", [Table],
        "Expressao", [Expression],
        "Lineage Tag", [LineageTag]
    )
VAR tabelas =
    SELECTCOLUMNS (
        FILTER (
            INFO.VIEW.TABLES (),
            [Name] <> "Cat치logo de Dados"
                && [Name] <> "Calculations"
                && NOT ( [IsHidden] )
        ),
        "Categoria", "Tabela",
        "Nome", [Name],
        "Descricao", [Description],
        "Tipo", SWITCH (
                TRUE (),
                NOT (ISBLANK ( [Expression] )), "Tabela calculada",
                "Tabela de dados"
            ),
        "Origem",[StorageMode],
        "Expressao", [Expression],
        "Lineage Tag", [LineageTag]
    )
VAR relacionamentos =
    SELECTCOLUMNS (
        INFO.VIEW.RELATIONSHIPS (),
        "Categoria", "Relacionamento",
        "Nome", [Relationship],
        "Descricao", IF(
                        [IsActive],
                        "Relacionamento Ativo.",
                        "Relacionamento Inativo."
                    ),
        "Tipo", [FromCardinality] & " To " & [ToCardinality] & " - " & [CrossFilteringBehavior],
        "Origem",
            "De: " & [FromTable] & "    Para: " & [ToTable],
        "Expressao", BLANK (),
        "Lineage Tag", [Name]
    )
RETURN
    UNION (
        colunas,
        medidas,
        tabelas,
        relacionamentos
    )